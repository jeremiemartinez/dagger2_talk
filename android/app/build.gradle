buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    // the latest version of the android-apt plugin
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def getIP() {
  InetAddress result = null;
  Enumeration<NetworkInterface> networkInterfaces = NetworkInterface.getNetworkInterfaces();
  while (networkInterfaces.hasMoreElements()) {
    Enumeration<InetAddress> addresses = networkInterfaces.nextElement().getInetAddresses();
    while (addresses.hasMoreElements()) {
      InetAddress address = addresses.nextElement();
      if (!address.isLoopbackAddress()) {
        if (address.isSiteLocalAddress()) {
          return address.getHostAddress();
        } else if (result == null) {
          result = address;
        }
      }
    }
  }
  return (result != null ? result : InetAddress.getLocalHost()).getHostAddress();
}

android {
  compileSdkVersion 23
  buildToolsVersion "22.0.1"

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude "META-INF/services/javax.annotation.processing.Processor"
  }

  defaultConfig {
    applicationId "fr.jmartinez.droidcon"
    minSdkVersion 21
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  buildTypes {
    debug {
      buildConfigField 'String', 'URL_SERVER', "\"http://${getIP()}:8080\""
    }
  }
}

dependencies {
  compile('com.android.support:appcompat-v7:23.0.1',
      'com.android.support:support-v4:23.0.1',
      'com.android.support:support-annotations:23.0.1',
      'de.greenrobot:eventbus:2.4.0',
      'com.squareup.retrofit:retrofit:1.9.0',
      'com.squareup.retrofit:retrofit-mock:1.9.0',
      'com.squareup.okhttp:okhttp-urlconnection:2.5.0',
      'com.squareup.okhttp:okhttp:2.5.0',
      'com.squareup.retrofit:converter-jackson:1.9.0',
      'com.fasterxml.jackson.core:jackson-databind:2.5.0',
      'com.fasterxml.jackson.core:jackson-core:2.5.0',
      'com.fasterxml.jackson.core:jackson-annotations:2.5.0',
      'com.jakewharton:butterknife:7.0.0',
      'com.jakewharton.timber:timber:2.5.0')

  compile 'com.google.dagger:dagger:2.0.1'
  apt 'com.google.dagger:dagger-compiler:2.0.1'
  provided 'org.glassfish:javax.annotation:10.0-b28'
}